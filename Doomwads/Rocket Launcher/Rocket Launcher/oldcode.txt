Actor HellstormCannon : Weapon
{
  Obituary "%o was obliterated by %k's HellStorm Cannon!"
  Weapon.SelectionOrder 2500
  Weapon.AmmoUse 1
  Weapon.AmmoGive 8
  Weapon.AmmoType "ImpactGrenade"
  +Weapon.NoAutoFire
  +Weapon.NoAutoAim
  Inventory.PickupMessage "You got the HellStorm Cannon"
  States
  {
  Ready:
    HSTM B 1 A_WeaponReady
    Loop
  Deselect:
    HSTM B 1 A_Lower
    Loop
  Select:
    HSTM B 1 A_Raise
    Loop
  Fire:
    HSTM C 4 Bright A_GunFlash
    HSTM D 0 Bright A_PlayWeaponSound("HellStorm/Fire")
    HSTM D 0 Bright A_FireCustomMissile("HellStormBomb1", Random(-9, 9), 0, 0, 0, 0, Random(-2, 6))
    HSTM D 0 Bright A_FireCustomMissile("HellStormBomb1", Random(-9, 9), 0, 0, 0, 0, Random(-2, 6))
    HSTM D 0 Bright A_FireCustomMissile("HellStormBomb2", Random(-9, 9), 0, 0, 0, 0, Random(-2, 6))
    HSTM D 0 Bright A_FireCustomMissile("HellStormBomb2", Random(-9, 9), 0, 0, 0, 0, Random(-2, 6))
    HSTM D 0 Bright A_FireCustomMissile("HellStormBomb3", Random(-9, 9), 0, 0, 0, 0, Random(-2, 6))
    HSTM D 0 Bright A_FireCustomMissile("HellStormBomb3", Random(-9, 9), 0, 0, 0, 0, Random(-2, 6))
    HSTM D 0 Bright A_FireCustomMissile("HellStormBomb4", Random(-9, 9), 0, 0, 0, 0, Random(-2, 6))
    HSTM D 5 Bright A_FireCustomMissile("HellStormBomb4", Random(-9, 9), 1, 0, 0, 0, Random(-2, 6))
    HSTM EF 6 Bright
    HSTM B 6
    HSTM B 45 A_PlaySoundEx("HellStorm/Reload", "SoundSlot6", 0)
    Goto Ready
  Flash:
    TNT1 A 1
    Stop
  Spawn:
    HSTM A -1
    Stop
  }
}

Actor HellStormBomb1
{
  Radius 11
  Height 8
  Speed 25
  Damage 10
  Gravity 0.3
  Scale 0.5
  Projectile
  -NoGravity
  DeathSound "HellStorm/Hit"
  Decal RevenantScorch
  States
  {
  Spawn:
    HSBM A 1 Bright
    Loop
  Death:
    MISL B 0 Bright A_ChangeFlag("NoGravity", 1)
    MISL B 8 Bright A_Explode(64, 64, 1)
    MISL C 6 Bright
    MISL D 4 Bright
    Stop
  }
}

Actor HellStormBomb2 : HellStormBomb1
{
  Speed 24
  Gravity 0.28
}

Actor HellStormBomb3 : HellStormBomb1
{
  Speed 23
  Gravity 0.26
}

Actor HellStormBomb4 : HellStormBomb1
{
  Speed 22
  Gravity 0.24
}

Actor ImpactGrenade : Ammo
{
  Inventory.PickupMessage "You got some impact grenades"
  Inventory.Amount 4
  Inventory.MaxAmount 40
  Ammo.BackpackAmount 4
  Ammo.BackpackMaxAmount 80
  Inventory.Icon "HSBMA7A3"
  States
  {
  Spawn:
    HSAM A -1
    Stop
  }
}

Actor ImpactGrenadeBox : ImpactGrenade
{
  Inventory.PickupMessage "You got a box of impact grenades"
  Inventory.Amount 20
  States
  {
  Spawn:
    HSAM B -1
    Stop
  }
}
